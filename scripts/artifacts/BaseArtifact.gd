## Provides base level interface for attaching behaviors to artifacts
## Extend if you require more complex logic
extends Resource
class_name BaseArtifact

var artifact_data: ArtifactData = null

func _init(_artifact_data: ArtifactData):
	artifact_data = _artifact_data
	connect_signals()
	
func connect_signals() -> void:
	# override with super()
	# set up signal connections for the artifact to listen to
	Signals.combat_ended.connect(_on_combat_ended)
	Signals.player_turn_started.connect(_on_player_turn_started)
	Signals.player_turn_ended.connect(_on_player_turn_ended)

 ## optional override
 ## performs any special logic on the player for when the artifact is added
 ## this should only be done when the artifact is acquired
func add_artifact() -> void:
	artifact_data.perform_artifact_actions(artifact_data.artifact_add_actions)

## optional override
## performs any special logic on the player for when the artifact is removed
func remove_artifact() -> void:
	artifact_data.perform_artifact_actions(artifact_data.artifact_remove_actions)

## Called from Artifact UI
func right_click_artifact() -> void:
	if not ActionHandler.actions_being_performed:
		if len(artifact_data.artifact_right_click_actions) > 0:
			artifact_data.perform_artifact_actions(artifact_data.artifact_right_click_actions)

func _on_combat_ended() -> void:
	# reset counter
	if artifact_data.artifact_counter_reset_on_combat_end >= 0:
		artifact_data.set_artifact_counter(artifact_data.artifact_counter_reset_on_combat_end)
	# end of combat actions
	artifact_data.perform_artifact_actions(artifact_data.artifact_end_of_combat_actions)

func _on_player_turn_started() -> void:
	# reset counter
	if artifact_data.artifact_counter_reset_on_turn_start >= 0:
		artifact_data.set_artifact_counter(artifact_data.artifact_counter_reset_on_turn_start)
	# first turn actions
	if Global.get_combat_stats().turn_count == 1:
		artifact_data.perform_artifact_actions(artifact_data.artifact_first_turn_actions)
	# normal start of turn actions
	artifact_data.perform_artifact_actions(artifact_data.artifact_turn_start_actions)

func _on_player_turn_ended() -> void:
	# end of turn actions
	artifact_data.perform_artifact_actions(artifact_data.artifact_turn_end_actions)
