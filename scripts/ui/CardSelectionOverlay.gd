# Provides an overlay to pick and view cards
extends Control

@onready var card_container: GridContainer = $ScrollContainer/MarginContainer/CardContainer
@onready var card_picking_label: Label = $CardPickLabel
@onready var confirm_button: Button = $ConfirmButton
@onready var back_button: Button = $BackButton

var current_card_pick_action: ActionBasePickCards = null	# an action currently requesting cards from the player to select. If null clicking cards plays them

enum CARD_MODES {VIEW, SELECT}
var card_mode: int = CARD_MODES.VIEW	# determines to view or select the card when a card is clicked 


func _ready():
	Signals.card_pick_requested.connect(_on_card_pick_requested)
	Signals.card_pick_confirmed.connect(_on_card_pick_confirmed)
	
	confirm_button.button_up.connect(_on_confirm_button_up)
	back_button.button_up.connect(_on_back_button_up)
	
	Signals.run_started.connect(_on_run_started)
	Signals.run_ended.connect(_on_run_ended)

func _on_card_pick_requested(card_pick_action: ActionBasePickCards):
	if card_pick_action != null:
		if ActionBasePickCards.DECK_PICK_TYPES.has(card_pick_action.get_card_pick_type()):
			set_card_mode(CARD_MODES.SELECT)
			current_card_pick_action = card_pick_action
			card_picking_label.text = current_card_pick_action.get_card_pick_text()
			confirm_button.visible = current_card_pick_action.are_enough_cards_picked()
			populate_cards(card_pick_action.get_pickable_cards())
		

func _on_card_pick_confirmed():
	visible = false
	current_card_pick_action = null

func populate_cards(cards: Array[CardData]) -> void:
	clear_cards()

	for card_data in cards:
		var card: Card = Scenes.CARD.instantiate()
		card_container.add_child(card)
		card.init(card_data, 0, false, true)
		
		# bind signals
		card.card_hovered.connect(_on_card_hovered)
		card.card_unhovered.connect(_on_card_unhovered)
		card.card_selected.connect(_on_card_selected)

func clear_cards():
	for child in card_container.get_children():
		child.queue_free()

func _on_card_hovered(_card: Card):
	pass

func _on_card_unhovered(_card: Card):
	pass

func _on_card_selected(card: Card):
	if card_mode == CARD_MODES.SELECT:
		if current_card_pick_action != null:
			# unpick card
			if current_card_pick_action.picked_cards.has(card.card_data):
				current_card_pick_action.picked_cards.erase(card.card_data) # remove from picked cards
				card.set_card_glow(false)
			# pick card
			else:
				# card can be picked
				if current_card_pick_action.is_card_pickable(card.card_data):
					current_card_pick_action.picked_cards.append(card.card_data)	# add to picked cards
					card.set_card_glow(true)
					
			card_picking_label.text = current_card_pick_action.get_card_pick_text()
			confirm_button.visible = current_card_pick_action.are_enough_cards_picked()
			
			# quick pick automatically confirms
			if current_card_pick_action.is_quick_pick():
				_on_confirm_button_up()
			
	else:
		pass

func _on_confirm_button_up():
	visible = false
	Signals.card_pick_confirmed.emit()

func _on_back_button_up():
	visible = false
	
### View mode wrappers

func set_card_mode(_card_mode: int) -> void:
	card_mode = _card_mode
	
	visible = true
	card_picking_label.visible = false
	back_button.visible = false
	confirm_button.visible = false
	
	if card_mode == CARD_MODES.VIEW:
		back_button.visible = true
	if card_mode == CARD_MODES.SELECT:
		confirm_button.visible = true
		card_picking_label.visible = true
	

func view_deck() -> void:
	set_card_mode(CARD_MODES.VIEW)
	populate_cards(Global.player_data.player_deck)

func view_draw_pile() -> void:
	set_card_mode(CARD_MODES.VIEW)
	# randomize the draw pile so player's can't see next cards
	var randomized_draw: Array[CardData] = Global.player_data.player_draw.duplicate(false)
	randomized_draw.shuffle() #NOTE: this doesn't need to be deterministic
	populate_cards(randomized_draw)

func view_discard() -> void:
	set_card_mode(CARD_MODES.VIEW)
	populate_cards(Global.player_data.player_discard)

func view_exhaust() -> void:
	set_card_mode(CARD_MODES.VIEW)
	populate_cards(Global.player_data.player_exhaust)
	
func _on_run_started():
	visible = false
func _on_run_ended():
	visible = false
	current_card_pick_action = null
