## UI component for a selectable option. Used for run start options and dialogue options.
## Supports rich text.
extends PanelContainer
class_name DialogueOption

@onready var rich_text_label = $RichTextLabel

## The dialogue option this button represents. Run start option buttons will have this as empty.
var dialogue_option_object_id: String = ""

var action_data: Array[Dictionary] = []
var validators: Array[Dictionary] = []
var option_enabled: bool = false

signal dialogue_option_clicked(dialogue_option: DialogueOption)

func _ready():
	gui_input.connect(_on_gui_input)

func init(_dialogue_option_object_id: String, option_bbcode: String, option_failed_validator_bbcode: String, _action_data: Array[Dictionary], _validators: Array[Dictionary]) -> void:
	dialogue_option_object_id = _dialogue_option_object_id
	action_data = _action_data
	validators = _validators
	option_enabled = validate_dialogue_option()
	if option_enabled:
		set_dialogue_bb_code(option_bbcode)
	else:
		set_dialogue_bb_code(option_failed_validator_bbcode)

func validate_dialogue_option() -> bool:
	# checks if option passes all validators
	return Global.validate(validators, null, null)

func set_dialogue_bb_code(bb_code: String) -> void:
	rich_text_label.parse_bbcode(bb_code)

func _on_gui_input(event: InputEvent):
	if option_enabled:
		if event.is_action_pressed("left_click"):
			dialogue_option_clicked.emit(self)
