# tooltip component which can display a list of keywords via KeywordTooltip
# attached to objects as a ui component; not dynamically instantiated
extends VBoxContainer

func populate_card_keywords(card_data: CardData) -> void:
	# wrapper function to call keywords on a card
	# automatically adds keywords to the list based on card flags
	var card_keyword_object_ids: Array[String] = card_data.card_keyword_object_ids
	
	if card_data.card_first_shuffle_priority > 0:
		if not card_keyword_object_ids.has("keyword_top_deck"):
			card_keyword_object_ids.append("keyword_top_deck")
	if card_data.card_first_shuffle_priority < 0:
		if not card_keyword_object_ids.has("keyword_bottom_deck"):
			card_keyword_object_ids.append("keyword_bottom_deck")
	
	if card_data.card_is_retained:
		if not card_keyword_object_ids.has("keyword_retain"):
			card_keyword_object_ids.append("keyword_retain")
	if card_data.card_is_ethereal:
		if not card_keyword_object_ids.has("keyword_ethereal"):
			card_keyword_object_ids.append("keyword_ethereal")
	if card_data.card_exhausts:
		if not card_keyword_object_ids.has("keyword_exhaust"):
			card_keyword_object_ids.append("keyword_exhaust")
	
	populate_keywords(card_keyword_object_ids)

func populate_keywords(keyword_object_ids: Array[String]) -> void:
	clear_keywords()
	var all_child_keywords: Array[String] = _get_all_recursive_child_keywords(keyword_object_ids)
	for keyword_object_id in all_child_keywords:
		var keyword_tooltip = Scenes.KEYWORD_TOOLTIP.instantiate()
		add_child(keyword_tooltip)
		keyword_tooltip.init(keyword_object_id)

func _get_all_recursive_child_keywords(keyword_object_ids: Array[String]) -> Array[String]:
	# searches (BFS) all child keywords and returns the full list
	# this is typically a shallow search but ensures all keywords are properly listed 
	var all_child_keywords: Array[String] = keyword_object_ids.duplicate()
	var i: int = 0
	while i < len(all_child_keywords):
		var keyword_object_id: String = all_child_keywords[i]
		var keyword_data: KeywordData = Global.get_keyword_data(keyword_object_id)
		if keyword_data == null:
			push_error("No keyword of id ", keyword_object_id, " found")
		else:
			for child_keyword_object_id in keyword_data.keyword_child_keyword_object_ids:
				if not all_child_keywords.has(child_keyword_object_id):
					all_child_keywords.append(child_keyword_object_id)
		i += 1
	return all_child_keywords

func clear_keywords() -> void:
	for child in get_children():
		child.queue_free()
