# Provides an overlay to add cards to the player's deck
extends Control

@onready var card_container: HBoxContainer = $CardContainer
@onready var skip_button: Button = $SkipButton

var current_card_pick_action: ActionBasePickCards = null	# an action currently requesting cards from the player to select. If null clicking cards plays them

func _ready():
	Signals.card_pick_requested.connect(_on_card_pick_requested)
	Signals.card_pick_confirmed.connect(_on_card_pick_confirmed)
	
	Signals.run_started.connect(_on_run_started)
	Signals.run_ended.connect(_on_run_ended)
	
	skip_button.button_up.connect(_on_skip_button_up)

func _on_card_pick_requested(card_pick_action: ActionBasePickCards):
	if card_pick_action != null:
		if card_pick_action.get_card_pick_type() == ActionBasePickCards.CARD_PICK_TYPES.DRAFT:
			current_card_pick_action = card_pick_action
			visible = true
			populate_cards(card_pick_action.get_pickable_cards())
		

func _on_card_pick_confirmed():
	visible = false
	current_card_pick_action = null

func populate_cards(cards: Array[CardData]) -> void:
	clear_cards()

	for card_data in cards:
		var card: Card = Scenes.CARD.instantiate()
		card_container.add_child(card)
		card.init(card_data, 0, false, true)
		
		# bind signals
		card.card_hovered.connect(_on_card_hovered)
		card.card_unhovered.connect(_on_card_unhovered)
		card.card_selected.connect(_on_card_selected)

func clear_cards():
	for child in card_container.get_children():
		child.queue_free()

func _on_card_hovered(_card: Card):
	pass

func _on_card_unhovered(_card: Card):
	pass

func _on_card_selected(card: Card):
	if current_card_pick_action != null:
		# only one card per draft; automatically end pick on first card selection
		current_card_pick_action.picked_cards.append(card.card_data)	# add to picked cards
		Signals.card_pick_confirmed.emit()	# finish card draft
		
		visible = false
		current_card_pick_action = null
		clear_cards()

func _on_skip_button_up():
	Signals.card_pick_confirmed.emit()	# finish card draft
	visible = false
	
func _on_run_started():
	visible = false
func _on_run_ended():
	visible = false
	current_card_pick_action = null
