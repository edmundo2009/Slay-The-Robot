extends Control

@onready var background_button: TextureButton = $%BackgroundButton
@onready var select_target_label: Label = %SelectTargetLabel
@onready var hand: Control = $%Hand

@onready var consumable_container: HBoxContainer = $ConsumableContainer
@onready var consumable_dropdown: Control = $ConsumableActionDropdown
@onready var use_consumable_button: Button = $ConsumableActionDropdown/UseConsumableButton
@onready var discard_consumable_button: Button = $ConsumableActionDropdown/DiscardConsumableButton

const NO_CONSUMABLE: int = -1
var selected_consumable_slot_index: int = NO_CONSUMABLE	# the currently selected slot 
var consumable_target_requested: bool = false

func _ready():
	background_button.button_up.connect(_on_background_button_up)
	use_consumable_button.button_up.connect(_on_use_consumable_button_up)
	discard_consumable_button.button_up.connect(_on_discard_consumable_button_up)
	
	Signals.enemy_clicked.connect(_on_enemy_clicked)
	
	Signals.consumable_used.connect(_on_consumable_used)
	Signals.consumable_added.connect(_on_consumable_added)
	Signals.consumable_discarded.connect(_on_consumable_discarded)
	
	Signals.add_consumable_requested.connect(_on_add_consumable_requested)
	
	Signals.combat_started.connect(_on_combat_started)
	Signals.combat_ended.connect(_on_combat_ended)
	Signals.run_started.connect(_on_run_started)
	Signals.run_ended.connect(_on_run_ended)

func populate_consumable_buttons():
	clear_consumable_buttons()
	
	var player_data: PlayerData = Global.player_data
	for consumable_slot_index in player_data.player_consumable_slot_count:
		var consumable_button: ConsumableButton = Scenes.CONSUMABLE_BUTTON.instantiate()
		consumable_container.add_child(consumable_button)
		consumable_button.init(consumable_slot_index)
		consumable_button.consumable_slot_button_up.connect(_on_consumable_button_up)
	
func clear_consumable_buttons() -> void:
	for child in consumable_container.get_children():
		child.queue_free()

func _on_consumable_button_up(consumable_slot_index: int):
	select_consumable_slot(consumable_slot_index)

func select_consumable_slot(consumable_slot_index: int) -> void:
	consumable_target_requested = false
	select_target_label.hide()
	if selected_consumable_slot_index != consumable_slot_index:
		# check if consumable in slot
		var consumable_data: ConsumableData = Global.get_player_consumable_in_slot_index(consumable_slot_index)
		if consumable_data != null:
			selected_consumable_slot_index = consumable_slot_index
			display_consumable_dropdown()
		else:
			selected_consumable_slot_index = NO_CONSUMABLE
			hide_consumable_dropdown()

func display_consumable_dropdown() -> void:
	consumable_dropdown.show()

func hide_consumable_dropdown() -> void:
	consumable_dropdown.hide()

func _on_background_button_up():
	# user clicked off ui, reset
	hide_consumable_dropdown()
	selected_consumable_slot_index = NO_CONSUMABLE

func _on_use_consumable_button_up():
	if not ActionHandler.actions_being_performed:
		if len(hand.card_play_queue) == 0:
			if is_consumable_selected():
				# check if consumable requires a target
				var consumable_data: ConsumableData = get_selected_consumable_data()
				if consumable_data != null:
					if consumable_data.consumable_requires_target:
						# prompt for target
						select_target_label.show()
						consumable_target_requested = true
					else:
						# automatically use consumable
						use_consumable(null, selected_consumable_slot_index)

func _on_discard_consumable_button_up():
	if is_consumable_selected():
		discard_consumable(selected_consumable_slot_index)

func _on_enemy_clicked(enemy: Enemy):
	if consumable_target_requested:
		if enemy.is_alive():
			if is_consumable_selected():
				use_consumable(enemy, selected_consumable_slot_index)
	
func _discard_consumable(consumable_slot_index: int = selected_consumable_slot_index) -> void:
	if is_consumable_selected():
		Global.player_data.discard_consumable(consumable_slot_index)

func is_consumable_selected() -> bool:
	return selected_consumable_slot_index != NO_CONSUMABLE

func get_selected_consumable_data() -> ConsumableData:
	return Global.get_player_consumable_in_slot_index(selected_consumable_slot_index)

func use_consumable(selected_target: BaseCombatant, consumable_slot_index: int = selected_consumable_slot_index) -> void:
	if is_consumable_selected():
		ActionGenerator.generate_use_consumable(selected_target, consumable_slot_index)

func add_consumable(consumable_object_id: String) -> void:
	# attempts to add a consumable, if enough slots are available
	var player_data: PlayerData = Global.player_data
	if not player_data.are_consumable_slots_full():
		# iterate over slots until an empty slot is found
		for consumable_slot_index: int in player_data.player_consumable_slot_count:
			if not player_data.player_consumable_slot_to_consumable_object_id.has(str(consumable_slot_index)):
				# add consumable to slot
				player_data.player_consumable_slot_to_consumable_object_id[str(consumable_slot_index)] = consumable_object_id
				Signals.consumable_added.emit(consumable_slot_index, consumable_object_id)
				return

func discard_consumable(consumable_slot_index: int) -> void:
	# attempts to remove a consumable at a give slot
	var player_data: PlayerData = Global.player_data
	var consumable_data: ConsumableData = Global.get_player_consumable_in_slot_index(str(consumable_slot_index))
	if consumable_data != null:
		player_data.player_consumable_slot_to_consumable_object_id.erase(str(consumable_slot_index))
		Signals.consumable_discarded.emit(consumable_slot_index, consumable_data.object_id)
		
func _on_add_consumable_requested(consumable_object_id: String):
	add_consumable(consumable_object_id)

func _on_consumable_used(_consumable_index: int, _consumable_object_id: String):
	select_consumable_slot(NO_CONSUMABLE)
	populate_consumable_buttons()
func _on_consumable_discarded(_consumable_index: int, _consumable_object_id: String):
	select_consumable_slot(NO_CONSUMABLE)
	populate_consumable_buttons()
func _on_consumable_added(_consumable_index: int, _consumable_object_id: String):
	select_consumable_slot(NO_CONSUMABLE)
	populate_consumable_buttons()

func _on_combat_ended():
	hide_consumable_dropdown()

func _on_combat_started(_event_id: String):
	hide_consumable_dropdown()

func _on_run_started():
	hide_consumable_dropdown()
	populate_consumable_buttons()

func _on_run_ended():
	hide_consumable_dropdown()
	clear_consumable_buttons()
