# ui component for displaying top cards in deck
extends BaseCustomUI

@onready var card_container = $CardContainer

const NUMBER_OF_DISPLAYED_CARDS: int = 5
const Y_OFFSET_PER_CARD: int = 32

const RERENDER_LAZILY: bool = true
var _is_rerendering: bool = false

func _ready():
	Signals.combat_ended.connect(_on_combat_ended)
	Signals.card_drawn.connect(_on_card_drawn)
	Signals.card_discarded.connect(_on_card_discarded)
	Signals.card_added_to_draw.connect(_on_card_added_to_draw)
	Signals.card_deck_shuffled.connect(_on_card_deck_shuffled)
	
func init(_custom_ui_object_id: String, _parent_combatant: BaseCombatant) -> void:
	custom_ui_object_id = _custom_ui_object_id
	parent_combatant = _parent_combatant
	_regenerate_displayed_cards()

func _regenerate_displayed_cards() -> void:
	# generates the display cards used to show top of draw pile
	if _is_rerendering:
		return
	if RERENDER_LAZILY:
		_is_rerendering = true
		await get_tree().process_frame
		_is_rerendering = false
	
	var player_draw: Array[CardData] = Global.player_data.player_draw
	var draw_pile_display_count: int = min(NUMBER_OF_DISPLAYED_CARDS, len(player_draw))
	
	_clear_displayed_cards()
	for i: int in draw_pile_display_count:
		var card_index: int = -(i + 1)
		var y_index: int = (draw_pile_display_count - 1) - i
		
		var card_data: CardData = player_draw[card_index]
		var card: Card = Scenes.CARD.instantiate()
		card_container.add_child(card)
		card.init(card_data, 0, false, false)
		card.position.y += Y_OFFSET_PER_CARD * y_index

func _clear_displayed_cards() -> void:
	for child in card_container.get_children():
		child.queue_free()

func _on_card_drawn(_card_data: CardData):
	_regenerate_displayed_cards()

func _on_card_discarded(_card_data: CardData, _is_manual_discard: bool):
	_regenerate_displayed_cards()

func _on_card_added_to_draw(_card_data: CardData):
	_regenerate_displayed_cards()

func _on_card_deck_shuffled(is_reshuffle: bool):
	if is_reshuffle:
		_regenerate_displayed_cards()
