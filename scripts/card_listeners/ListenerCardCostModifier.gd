# Provides a general way to modify a card's energy based on a provided combat stat
# ex -1 energy per draw this turn, +2 energy per damage taken this combat
extends BaseCardListener

var stat_enum: int = CombatStatsData.STATS.CARDS_DISCARDED

func _init(_parent_card: Card, _values: Dictionary = {}):
	super._init(_parent_card, _values)
	stat_enum = values.get("stat_enum", CombatStatsData.STATS.CARDS_DISCARDED)

func _connect_signals():
	Signals.card_drawn.connect(_on_card_drawn)
	Signals.combat_stat_changed.connect(_on_combat_stat_changed)

func _on_card_drawn(_card_data: CardData):
	# keep, so card's cost updates on draw
	if card_data == _card_data:
		_update_card_cost()

func _on_combat_stat_changed(_stat_enum: int):
	if _stat_enum == stat_enum:
		_update_card_cost()

func _update_card_cost() -> void:
	var energy_per_stat: int = values.get("energy_per_stat", -1)	# can be positive or negative
	var is_turn_stat: bool = values.get("is_turn_stat", true)
	
	# flags for which energy stats to modify
	var modifiy_card_energy_cost_until_combat: bool = values.get("modifiy_card_energy_cost_until_combat", false)
	var modifiy_card_energy_cost_until_played: bool = values.get("modifiy_card_energy_cost_until_played", false)
	var modifiy_card_energy_cost_until_turn: bool = values.get("modifiy_card_energy_cost_until_turn", false)
	
	# get the stat and its value
	var combat_stats: CombatStatsData = Global.player_data.player_current_combat_stats
	var stat: int
	if is_turn_stat:
		stat = combat_stats.get_turn_stat(stat_enum)
	else:
		stat = combat_stats.get_total_stat(stat_enum)
	
	# calculate new card cost and determine if it requires changing
	var raw_card_cost: int = parent_card.card_data.get_card_energy_cost(false)
	var new_card_cost: int = max(0, raw_card_cost + (stat * energy_per_stat))
	if new_card_cost != parent_card.card_data.get_card_energy_cost(true):
		# compile energy cost action's values
		var action_values: Dictionary = {"picked_cards": [parent_card.card_data]}
		if modifiy_card_energy_cost_until_combat:
			action_values["card_energy_cost_until_combat"] = new_card_cost
		if modifiy_card_energy_cost_until_played:
			action_values["card_energy_cost_until_played"] = new_card_cost
		if modifiy_card_energy_cost_until_turn:
			action_values["card_energy_cost_until_turn"] = new_card_cost
		
		# generate card energy action
		var action_data: Array[Dictionary] = [{
			Scripts.ACTION_CHANGE_CARD_ENERGIES: action_values
			}]
		var generated_action: BaseAction = ActionGenerator.create_actions(null, null, [], action_data, null)[0]
		
		# immediately process this action without ActionHandler
		generated_action.perform_action()
