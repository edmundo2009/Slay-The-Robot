# Provides a general way to modify a card's card_values based on a provided combat stat
# ex -1 damage per draw this turn, +2 energy per damage taken this combat
# This is applied as a function, not relatively, so the value is recalculated each time
# For action wrapper variant, see ActionVariableCombatStatsModifier
# This mutates the card_values, so its effect can be seen in the card description
extends BaseCardListener

var stat_enum: int = CombatStatsData.STATS.CARDS_DISCARDED

func _init(_parent_card: Card, _values: Dictionary = {}):
	super._init(_parent_card, _values)
	stat_enum = values.get("stat_enum", CombatStatsData.STATS.CARDS_DISCARDED)

func _connect_signals():
	Signals.player_turn_started.connect(_on_player_turn_started)
	Signals.card_drawn.connect(_on_card_drawn)
	Signals.combat_stat_changed.connect(_on_combat_stat_changed)

func _on_player_turn_started():
	# keep, so card's values updates on turn start
	_update_card_values()

func _on_card_drawn(_card_data: CardData):
	# keep, so card's cost updates on draw
	if card_data == _card_data:
		_update_card_values()

func _on_combat_stat_changed(_stat_enum: int):
	if _stat_enum == stat_enum:
		_update_card_values()

func _update_card_values() -> void:
	var multiplied_values: Array = values.get("multiplied_values", [])	# the key names of the values of the card multiplied by this listener
	var multiplied_values_bases: Dictionary = values.get("multiplied_values_bases", {})
	var multiplied_values_per_stat: Dictionary = values.get("multiplied_values_per_stat", {})
	var multiplied_values_min: Dictionary = values.get("multiplied_values_min", {})	# if a key is provided, will bind the value to a minimum after calculation
	var multiplied_values_max: Dictionary = values.get("multiplied_values_max", {})	# if a key is provided, will bind the value to a maximum after calculation
	
	var is_turn_stat: bool = values.get("is_turn_stat", true)
	
	# get the stat and its value
	var combat_stats: CombatStatsData = Global.player_data.player_current_combat_stats
	var stat_value: int = 0
	if is_turn_stat:
		stat_value = combat_stats.get_turn_stat(stat_enum)
	else:
		stat_value = combat_stats.get_total_stat(stat_enum)
	
	# calculate each card value from the stat
	var card_values: Dictionary = parent_card.card_data.card_values.duplicate(true)
	for value_key: String in card_values.keys():
		if value_key in multiplied_values:
			var base_value: int = multiplied_values_bases.get(value_key, 0)
			var value_per_stat: int = multiplied_values_per_stat.get(value_key, 0)
			var value: int = card_values[value_key]
			card_values[value_key] = base_value + (stat_value * value_per_stat)
			
			# clamp min
			if multiplied_values_min.has(value_key):
				card_values[value_key] = min(card_values[value_key], multiplied_values_min[value_key])
			# clamp max
			if multiplied_values_max.has(value_key):
				card_values[value_key] = max(card_values[value_key], multiplied_values_max[value_key])
	
	# update the card's values
	parent_card.card_data.card_values = card_values
	Signals.card_properties_changed.emit(parent_card.card_data)
