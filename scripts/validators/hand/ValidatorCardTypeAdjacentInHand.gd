## Validator that checks to see if a card is adjacent to cards with given card types.
## Fails if card not in hand.
extends BaseValidator

func _validation(_card_data: CardData, _action: BaseAction, values: Dictionary[String, Variant]) -> bool:
	var card_types: Array[int] = []
	card_types.assign(_get_validator_value("card_types", values, _action, []))
	# requires both cards match criteria
	var requires_surrounded: bool = _get_validator_value("requires_surrounded", values, _action, false)
	
	var card_data: CardData = _card_data
	var hand: Array[CardData] = Global.player_data.player_hand
	
	# take the card and hand from action if one provided
	if _action != null:
		if _action.card_play_request != null:
			card_data = _action.card_play_request.card_data
			hand = _action.card_play_request.hand_at_play_time
	
	# no card provided to validator
	if card_data == null:
		return false
	
	# card must be in hand
	if not Global.player_data.player_hand.has(_card_data):
		return false
	
	# position of the card in hand
	var card_index: int = hand.find(card_data)
	var adjacent_cards: Array[CardData] = []
	
	# get left card
	if card_index > 0:
		var left_card_data: CardData = hand[card_index - 1]
		adjacent_cards.append(left_card_data)
	
	# get right card
	if card_index + 1 < len(hand):
		var right_card_data: CardData = hand[card_index + 1]
		adjacent_cards.append(right_card_data)
	
	# count the cards matching criteria
	var adjacency_counter: int = 0
	for adjacent_card: CardData in adjacent_cards:
		if card_types.has(adjacent_card.card_type):
			adjacency_counter += 1
	
	if adjacency_counter == 0:
		return false
	if adjacency_counter == 1 and requires_surrounded:
		return false
		
	return true
