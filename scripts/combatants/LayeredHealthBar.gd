# provides a layered healthbar that supports mutliple types of incoming damage, with animations
# each layer is superimposed, going from 0 to a cap, which provides a segmented effect
# See: BaseStatusEffect.get_status_healthbar_reserved_amount() and StatusEffectData.status_effect_healthbar_layer_color 
extends Control
class_name LayeredHealthBar

@onready var health_layer = $HealthLayer
@onready var damage_layer = $DamageLayer
@onready var damage_timer: Timer = $DamageLayer/DamageTimer
@onready var health_bar_text = $HealthBarText
@onready var status_layers = $StatusLayers
var damage_tween: Tween = null


var old_health: float = 0 # used for tweening
var new_health: float = 0 # used for tweening
var old_health_max: float = 0 # used for tweening

const DAMAGE_DELAY: float = 0.75 # delay before health will start tweening
const DAMAGE_TWEEN_TIME: float = 0.25

const HEALTH_RERENDERS_LAZILY: bool = true
var _health_is_rerendering: bool = false

func _ready():
	damage_timer.timeout.connect(_on_damage_delay_timeout)

func init(health: int, health_max: int) -> void:
	new_health = health
	old_health = health
	old_health_max = health_max
	health_bar_text.text = str(health) + "/" + str(health_max)
	health_layer.anchor_right = 0.0
	update_health_layers(health, health_max, {})
	damage_layer.anchor_right = 0.0

func apply_damage(health: int, health_max: int, status_id_to_status_effects: Dictionary) -> void:
	# main public method for messing with the health bar
	# updates health layers and applies a damaging animation to the healthbar
	if health == old_health and old_health_max == health_max:
		return # no changes

	new_health = health
	health_max = health_max
	
	if damage_timer.time_left > 0:
		damage_timer.start(DAMAGE_DELAY)
	else:
		damage_timer.start(DAMAGE_DELAY)
		damage_layer.visible = true
		var right_anchor: float = float(old_health) / max(1, float(health_max))
		damage_layer.anchor_right = right_anchor
	
	update_health_layers(health, health_max, status_id_to_status_effects)

func update_health_layers(health: int, health_max: int, status_id_to_status_effects: Dictionary) -> void:
	# visually updates the health and status layers
	# does not change state
	
	if _health_is_rerendering:
		return
	if HEALTH_RERENDERS_LAZILY:
		_health_is_rerendering = true
		await get_tree().process_frame
		_health_is_rerendering = false
	
	# main health bar
	_update_normal_health(health, health_max)
	
	# delete status layers
	for layer in status_layers.get_children():
		layer.queue_free()
	
	# generate new layers
	var health_right_endpoint: int = new_health
	var health_left_endpoint: int = new_health
	for status_effect_object_id: String in status_id_to_status_effects.keys():
		var status_effect_data: StatusEffectData = Global.get_status_effect_data(status_effect_object_id)
		var status_effects: Array[StatusEffect] = status_id_to_status_effects[status_effect_object_id]
		
		# get the width of the layer
		var layer_width: int = 0
		for status_effect: StatusEffect in status_effects:
			var status_effect_script: BaseStatusEffect = status_effect.status_effect_script
			layer_width = status_effect_script.get_status_healthbar_reserved_amount()
		# guard cases
		if layer_width <= 0:
			continue
		if health_right_endpoint <= 0:
			return # can't move past zero, don't display any more layers
		# adjust left endpoint leftward
		health_left_endpoint = max(0, health_left_endpoint - layer_width)
		# generate the layer
		var status_layer = Scenes.HEALTH_LAYER.instantiate()
		status_layers.add_child(status_layer)
		status_layer.init(status_effect_data.status_effect_healthbar_layer_color)
		var right_anchor = float(health_right_endpoint) / max(1, float(old_health_max))
		var left_anchor = float(health_left_endpoint) / max(1, float(old_health_max))
		status_layer.anchor_right = right_anchor
		status_layer.anchor_left = left_anchor
		
		health_right_endpoint = health_left_endpoint # right endpoint becomes old left endpoint

func _update_normal_health(health: int, health_max: int):
	var right_anchor: float = float(health) / max(1, float(health_max))
	health_layer.anchor_right = right_anchor
	health_bar_text.text = str(health) + "/" + str(health_max)

func _on_damage_delay_timeout():
	# damage bar will tween to match new health
	damage_layer.anchor_right = float(old_health) / max(1, float(old_health_max))
	var new_anchor_right: float = float(new_health) / max(1, float(old_health_max))
	old_health = new_health
	
	var damage_tween: Tween = create_tween()
	damage_tween.tween_property(damage_layer, "anchor_right", new_anchor_right, DAMAGE_TWEEN_TIME)
	damage_tween.tween_callback(_on_damage_tween_ended)
	
func _on_damage_tween_ended():
	damage_layer.visible = false
