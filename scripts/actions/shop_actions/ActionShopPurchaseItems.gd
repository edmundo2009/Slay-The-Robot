# allows purchasing things from a shop
extends BaseAction

func perform_action():
	# intercept the action
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([])
	for action_interceptor_processor in action_interceptor_processors:
		# ids for items to purchase; mutually exclusive
		var card_data: CardData = action_interceptor_processor.get_shadowed_action_values("card_data", null)
		var consumable_object_id: String = action_interceptor_processor.get_shadowed_action_values("consumable_object_id", "")
		var consumable_slot_index: int = action_interceptor_processor.get_shadowed_action_values("consumable_slot_index", 0)
		var artifact_id: String = action_interceptor_processor.get_shadowed_action_values("artifact_id", "")
		
		# check if shop at player location
		var shop_data: ShopData = Global.get_shop_at_player_location()
		if shop_data != null:
			var player_money: int = Global.player_data.player_money
			var player: Player = Global.get_player()
			var action_data: Array[Dictionary] = []
			
			# purchasing cards
			if card_data != null:
				var card_price: int = shop_data.get_shop_card_price(card_data)
				if player_money >= card_price:
					# generate action data for buying card
					action_data.append({Scripts.ACTION_ADD_MONEY: {"money_amount": -card_price}})
					action_data.append({Scripts.ACTION_ADD_CARDS_TO_DECK: {"picked_cards": [card_data]}})
					var generated_actions: Array[BaseAction] = ActionGenerator.create_actions(player, null, [], action_data, null)
					ActionHandler.add_actions(generated_actions)
					# remove card from shop
					shop_data.remove_shop_card(card_data)
					Signals.card_purchased.emit(card_data)
				return
			
			# purchasing artifacts
			if artifact_id != "":
				var artifact_price: int = shop_data.get_shop_artifact_price(artifact_id)
				if player_money >= artifact_price:
					# generate action data for buying artifact
					var artifact_data: ArtifactData = Global.get_artifact_data_from_prototype(artifact_id)
					action_data.append({Scripts.ACTION_ADD_MONEY: {"money_amount": -artifact_price}})
					action_data.append({Scripts.ACTION_ADD_ARTIFACT: {"artifact_id": artifact_id}})
					var generated_actions: Array[BaseAction] = ActionGenerator.create_actions(player, null, [], action_data, null)
					ActionHandler.add_actions(generated_actions)
					# remove artifact from shop
					shop_data.remove_shop_artifact(artifact_id)
					Signals.artifact_purchased.emit(artifact_data)
				return
			
			# purchasing consumables
			if consumable_object_id != "":
				var consumable_price: int = shop_data.get_shop_consumable_price(consumable_slot_index)
				if player_money >= consumable_price:
					if not Global.player_data.are_consumable_slots_full():
						# generate action data for buying consumable
						action_data.append({Scripts.ACTION_ADD_MONEY: {"money_amount": -consumable_price}})
						action_data.append({Scripts.ACTION_ADD_CONSUMABLE: {"consumable_object_id": consumable_object_id, "fill_all_slots": false, "random_consumable": false}})
						var generated_actions: Array[BaseAction] = ActionGenerator.create_actions(player, null, [], action_data, null)
						ActionHandler.add_actions(generated_actions)
						# remove consumable from shop
						shop_data.remove_shop_consumable(consumable_slot_index)
						Signals.consumable_purchased.emit(consumable_object_id)
					return
