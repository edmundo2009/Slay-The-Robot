# plays given cards
extends BaseCardsetAction

func perform_action() -> void:
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([])
	
	for action_interceptor_processor in action_interceptor_processors:
		var picked_cards: Array[CardData] = _get_picked_cards().duplicate()
		picked_cards.reverse()	# reverse the order so the enqueue is in correct order
		for card_data in picked_cards:
			# get a random enemy for each card play
			var enemies: Array[Node] = Global.get_tree().get_nodes_in_group("enemies")
			
			# get a targeting rng
			var rng_name: String = action_interceptor_processor.get_shadowed_action_values("rng_name", "rng_targeting")
			var rng_targeting: RandomNumberGenerator = Global.player_data.get_player_rng(rng_name)
			
			enemies = Random.shuffle_array(rng_targeting, enemies)
			
			var random_enemy: Enemy = null
			if len(enemies) > 0:
				random_enemy = enemies[0]
			
			
			# generate the card play request and enqueue it
			var new_card_play_request: CardPlayRequest = CardPlayRequest.new()
			new_card_play_request.card_data = card_data
			new_card_play_request.selected_target = random_enemy
			new_card_play_request.refundable_energy = 0
			new_card_play_request.input_energy = Global.player_data.player_energy
			new_card_play_request.card_values = card_play_request.card_values.duplicate(true)
			
			Signals.card_play_requested.emit(new_card_play_request, false, true)
