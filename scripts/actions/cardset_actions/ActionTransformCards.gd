# Transforms a list of cards into another kind of card
# This can target a list of cards, or their parent cards (making it permanent if in player's deck)
extends BaseCardsetAction

func perform_action():
	var picked_cards: Array[CardData] = _get_picked_cards()
	
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([])
	for action_interceptor_processor in action_interceptor_processors:
		# whether to transform the combat card or permanent card
		var transform_parent_card: bool = action_interceptor_processor.get_shadowed_action_values("transform_parent_card", true)
		# use this flag for a specific card id. Empty string for random transform
		var transform_into_card_object_id: String = action_interceptor_processor.get_shadowed_action_values("transform_into_card_object_id", "")
		# use upgrade of original card
		var keep_upgrade_level: bool = action_interceptor_processor.get_shadowed_action_values("keep_upgrade_level", false)
		# force transformed card to be upgraded to a level, -1 for no force
		var force_upgrade_level: int = action_interceptor_processor.get_shadowed_action_values("force_upgrade_level", -1)
		### randomized tranform params
		# limits the kind of card each card can transform into
		var keep_rarity: bool = action_interceptor_processor.get_shadowed_action_values("keep_rarity", false)
		var keep_color: bool = action_interceptor_processor.get_shadowed_action_values("keep_color", true)
		var keep_type: bool = action_interceptor_processor.get_shadowed_action_values("keep_type", false)
		# if keep flags are not used, use these
		var transform_rarities: Array[int] = []
		transform_rarities.assign(action_interceptor_processor.get_shadowed_action_values("transform_rarities", CardData.CARD_RARITIES.values()))
		var transform_colors: Array[String] = []
		transform_colors.assign(action_interceptor_processor.get_shadowed_action_values("transform_colors", Global._id_to_color_data.keys()))
		var transform_types: Array[int] = []
		transform_types.assign(action_interceptor_processor.get_shadowed_action_values("transform_types", CardData.STANDARD_CARD_TYPES))
		
		# iterate over the cards, transforming them and/or their parent
		for card_data in picked_cards:
			# get parent card if transforming that
			var parent_card_data: CardData = null
			if transform_parent_card:
				if card_data.parent_card == null:
					push_error("No parent card found")
					return
				else:
					parent_card_data = card_data.parent_card
			
			
			### find out which kind of card to transform into
			var new_card_object_id: String = transform_into_card_object_id
			# use a randomized search if no id provided
			if transform_into_card_object_id == "":
				var card_rarities: Array[int] = transform_rarities
				var card_colors: Array[String] = transform_colors
				var card_types: Array[int] = transform_types
				if keep_rarity:
					card_rarities = [card_data.card_rarity]
				if keep_color:
					card_colors = [card_data.card_color_id]
				if keep_type:
					card_types = [card_data.card_type]
				
				var card_object_ids: Array[String] = (
					CardFilter.new()
					.filter_rarity(card_rarities)
					.filter_colors(card_colors)
					.filter_type(card_types)
					.convert_to_card_object_ids()
					)
				
				var rng_name: String = action_interceptor_processor.get_shadowed_action_values("rng_name", "rng_card_transforming")
				var rng_card_transforming: RandomNumberGenerator = Global.player_data.get_player_rng(rng_name)	
				
				card_object_ids = Random.shuffle_array(rng_card_transforming, card_object_ids)
				
				if len(card_object_ids) == 0:
					push_error("Insufficient cards matching given transform criteria")
					return
				else:
					new_card_object_id = card_object_ids[0]
			
			### transform and upgrade the cards
			if card_data != null:
				var upgrade_level: int = 0
				if keep_upgrade_level:
					upgrade_level = card_data.card_upgrade_amount
				if force_upgrade_level >= 0:
					upgrade_level = force_upgrade_level
				
				card_data.transform_card(new_card_object_id)
				
				for i in upgrade_level:
					card_data.upgrade_card()
			
			if parent_card_data != null:
				var upgrade_level: int = 0
				if keep_upgrade_level:
					upgrade_level = parent_card_data.card_upgrade_amount
				if force_upgrade_level >= 0:
					upgrade_level = force_upgrade_level
				
				Global.player_data.transform_card_in_deck(parent_card_data, new_card_object_id)
				
				for i in upgrade_level:
					parent_card_data.upgrade_card()

func _to_string():
	return "Transform Card Action"
