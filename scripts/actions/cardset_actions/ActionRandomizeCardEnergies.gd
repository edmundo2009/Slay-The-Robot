# Randomizes the energy cost values of given cards
extends BaseCardsetAction

func perform_action() -> void:
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([])
	
	for action_interceptor_processor in action_interceptor_processors:
		# flags for what card energies to randomize
		var randomize_card_energy_cost: bool = action_interceptor_processor.get_shadowed_action_values("randomize_card_energy_cost", false)
		var randomize_card_energy_cost_until_combat: bool = action_interceptor_processor.get_shadowed_action_values("randomize_card_energy_cost_until_combat", false)
		var randomize_card_energy_cost_until_played: bool = action_interceptor_processor.get_shadowed_action_values("randomize_card_energy_cost_until_played", false)
		var randomize_card_energy_cost_until_turn: bool = action_interceptor_processor.get_shadowed_action_values("randomize_card_energy_cost_until_turn", false)
		
		# card cost random bounds (inclusive)
		var card_cost_min: int = action_interceptor_processor.get_shadowed_action_values("card_cost_min", 0)
		var card_cost_max: int = action_interceptor_processor.get_shadowed_action_values("card_cost_max", 3)
		
		# get an energy rng
		var rng_name: String = action_interceptor_processor.get_shadowed_action_values("rng_name", "rng_engergy_cost")
		var rng_engergy_cost: RandomNumberGenerator = Global.player_data.get_player_rng(rng_name)
		
		# randomize all card costs in cardset
		var picked_cards: Array[CardData] = _get_picked_cards()
		for card_data in picked_cards:
			if randomize_card_energy_cost:
				var random_card_cost: int = rng_engergy_cost.randi_range(card_cost_min, card_cost_max)
				card_data.set_card_energy_cost(random_card_cost)
			if randomize_card_energy_cost_until_combat:
				var random_card_cost: int = rng_engergy_cost.randi_range(card_cost_min, card_cost_max)
				card_data.set_card_energy_cost_until_combat(random_card_cost)
			if randomize_card_energy_cost_until_played:
				var random_card_cost: int = rng_engergy_cost.randi_range(card_cost_min, card_cost_max)
				card_data.set_card_energy_cost_until_played(random_card_cost)
			if randomize_card_energy_cost_until_turn:
				var random_card_cost: int = rng_engergy_cost.randi_range(card_cost_min, card_cost_max)
				card_data.set_card_energy_cost_until_turn(random_card_cost)
