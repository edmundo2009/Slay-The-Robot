## Changes the types of cards available to the player for future card rewards.
## Forces a recompiling of PlayerData.player_reward_card_filter_cache
extends BaseAction

func perform_action():
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([])
	for action_interceptor_processor: ActionInterceptorProcessor in action_interceptor_processors:
		# option to reset to character's starting card packs
		var reset_to_starting_card_packs: bool = action_interceptor_processor.get_shadowed_action_values("reset_to_starting_card_packs", false)
		if reset_to_starting_card_packs:
			Global.player_data.reward_draft_card_pack_ids = []
			var character_data: CharacterData = Global.get_character_data(Global.player_data.player_character_object_id)
			Global.player_data.reward_draft_card_pack_ids.assign(character_data.character_starting_card_draft_card_pack_ids)
		
		# option to reset to character's starting card packs
		var remove_all_card_packs: bool = action_interceptor_processor.get_shadowed_action_values("remove_all_card_packs", false)
		if remove_all_card_packs:
			Global.player_data.reward_draft_card_pack_ids = []
		
		# adding card packs
		var add_card_pack_object_ids: Array[String] = []
		add_card_pack_object_ids.assign(action_interceptor_processor.get_shadowed_action_values("add_card_pack_object_ids", []))
		
		for card_pack_object_id: String in add_card_pack_object_ids:
			if not Global.player_data.reward_draft_card_pack_ids.has(card_pack_object_id):
				Global.player_data.reward_draft_card_pack_ids.append(card_pack_object_id)
		
		# removing card packs
		var remove_card_pack_object_ids: Array[String] = []
		remove_card_pack_object_ids.assign(action_interceptor_processor.get_shadowed_action_values("remove_card_pack_object_ids", []))
		
		for card_pack_object_id: String in remove_card_pack_object_ids:
			Global.player_data.reward_draft_card_pack_ids.erase(card_pack_object_id)
		
		# whitelist card ids
		var whitelist_card_object_ids: Array[String] = []
		whitelist_card_object_ids.assign(action_interceptor_processor.get_shadowed_action_values("whitelist_card_object_ids", []))
		
		for whitelist_card_object_id: String in whitelist_card_object_ids:
			if not Global.player_data.player_reward_draft_card_id_whitelist.has(whitelist_card_object_id):
				Global.player_data.player_reward_draft_card_id_whitelist.append(whitelist_card_object_id)
			# remove blacklisted cards if whitelisted
			if Global.player_data.player_event_blacklisted_ids.has(whitelist_card_object_id):
				Global.player_data.player_event_blacklisted_ids.erase(whitelist_card_object_id)
		
		# blacklist card ids
		var blacklist_card_object_ids: Array[String] = []
		blacklist_card_object_ids.assign(action_interceptor_processor.get_shadowed_action_values("blacklist_card_object_ids", []))
		
		for blacklist_card_object_id: String in blacklist_card_object_ids:
			if not Global.player_data.player_reward_draft_card_id_blacklist.has(blacklist_card_object_id):
				Global.player_data.player_reward_draft_card_id_blacklist.append(blacklist_card_object_id)
			# remove whitelisted cards if blacklisted
			if Global.player_data.player_reward_draft_card_id_whitelist.has(blacklist_card_object_id):
				Global.player_data.player_reward_draft_card_id_whitelist.erase(blacklist_card_object_id)
		
		# apply update to player drafting
		Global.player_data.regenerate_card_draft_card_filter()
