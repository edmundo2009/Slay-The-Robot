# Adds rewards to the RewardOverlay
# Mainly an interceptable data payload
# Note: one is automatically created at the start of combat by RewardOverlay and cycled back to prepopulate end of combat rewards
extends BaseAction

func perform_action():
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([])
	
	for action_interceptor_processor in action_interceptor_processors:
		# reward groups allow mutually exclusive rewards. 0 is standard, -1 for auto create new group, positive for specific groups
		var reward_group: int = action_interceptor_processor.get_shadowed_action_values("reward_group", 0)
		var money_amount: int = action_interceptor_processor.get_shadowed_action_values("money_amount", 0)
		# array of array of CardData
		var card_drafts: Array[Array] = action_interceptor_processor.get_shadowed_action_values("card_drafts", [])
		var artifact_ids: Array[String] = action_interceptor_processor.get_shadowed_action_values("artifact_ids", [])
		# custom_action_data allows defining of unique reward actions through extensible data payloads
		# This would allow a card, artifact, or status to define a unique reward outside the standard ones
		# Example of a payload that adds a reward button to heal the user
		# Format is
		# {
		# "heal_reward": 
			# {
			# "reward_button_text": "Heal"
			# "reward_button_texture_path": "filepath.png"
			# "reward_button_values": {"health_amount: 5"},
			# "reward_button_actions": [{Scripts.ACTION_ADD_HEALTH: {}}]
			#}
		# }
		# Note: multiple keys can define multiple butttons
		# Each element in first array layer is a unique 
		var custom_action_data: Array[Array] = []
		custom_action_data.assign(action_interceptor_processor.get_shadowed_action_values("custom_action_data", []))
		
		Signals.reward_grant_requested.emit(reward_group, money_amount, card_drafts, artifact_ids, custom_action_data)

func _to_string():
	return "Grant Reward Action"
