## Increases/Descreases the charges for a given artifact(s). Can target either an artifact type or a specific
## instance of an artifact
extends BaseAction

func perform_action():
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([])
	for action_interceptor_processor in action_interceptor_processors:
		var artifact_id: String = action_interceptor_processor.get_shadowed_action_values("artifact_id", "")
		var artifact_charge_increase: int = action_interceptor_processor.get_shadowed_action_values("artifact_charge_increase", 1)
		
		# increment artifacts of a specific id (technically allows duplicates)
		if artifact_id != "":
			var artifacts_with_artifact_id: Array[ArtifactData] = Global.player_data.get_player_artifacts_with_artifact_id(artifact_id)
			for artifact_data: ArtifactData in artifacts_with_artifact_id:
				artifact_data.increment_artifact_counter(artifact_charge_increase)
		
		# increment a specific artifact.
		# This is usually passed in from the artifact itself automatically from the artifact's
		# event related actions, via BaseArtifact._perform_artifact_actions(), but other things may
		# pass it in as well
		var artifact_data: ArtifactData = action_interceptor_processor.get_shadowed_action_values("artifact_data", null)
		if artifact_data != null:
			artifact_data.increment_artifact_counter(artifact_charge_increase)

func is_instant_action() -> bool:
	return true

func _to_string():
	var artifact_charge_increase: int = get_action_value("artifact_charge_increase", 0)
	return "Increase Artifact Charge Action" + str(artifact_charge_increase)
