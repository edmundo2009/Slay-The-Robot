# This action does not do damage itself, rather it generates damaging actions which are placed immediately after on the stack
# Use this action instead of just invoking an AttackDamage action
extends BaseAction

func is_instant_action() -> bool:
	return true

func perform_action(): 
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([])
	
	for action_interceptor_processor in action_interceptor_processors:
		if parent_combatant != null:
			parent_combatant.play_attack_animation()
		
		var damage: int = action_interceptor_processor.get_shadowed_action_values("damage", 0)
		var delay: float = action_interceptor_processor.get_shadowed_action_values("time_delay", 0.25)
		var number_of_attacks: int = action_interceptor_processor.get_shadowed_action_values("number_of_attacks", 1)
		var merge_attacks: bool = action_interceptor_processor.get_shadowed_action_values("merge_attacks", false)	# this will take all attacks and merge them into a single attack with combined damage
		var target_override: int = action_interceptor_processor.get_shadowed_action_values("target_override", BaseAction.TARGET_OVERRIDES.SELECTED_TARGETS)
		
		var actions_on_lethal: Array[Dictionary] = []
		actions_on_lethal.assign(action_interceptor_processor.get_shadowed_action_values("actions_on_lethal", []))
		
		# generate a random number to add to damage if it exists
		var damage_random: int = action_interceptor_processor.get_shadowed_action_values("damage_random", 0)
		if damage_random > 1:
			var rng_damage_name: String = get_action_value("rng_damage_name", "rng_damage")
			var rng_damage: RandomNumberGenerator = Global.player_data.get_player_rng(rng_damage_name)
			var random_damage_amount: int = rng_damage.randi_range(0, damage_random)
			# add the random damage to the base damage
			damage += random_damage_amount
		
		if merge_attacks:
			damage = number_of_attacks * damage
			number_of_attacks = 1
		
		# generate the individual attack actions
		var generated_attack_actions: Array[BaseAction] = []
		for i in number_of_attacks:
			var action_data: Array[Dictionary] = [{Scripts.ACTION_ATTACK: {"damage": damage, "time_delay": delay, "target_override": target_override, "actions_on_lethal": actions_on_lethal}}]
			var attack_action: Array[BaseAction] = ActionGenerator.create_actions(parent_combatant, card_play_request, targets, action_data, self)
			generated_attack_actions += attack_action
		
		ActionHandler.add_actions(generated_attack_actions)

func _to_string():
	var damage: int = get_action_value("damage", 0)
	var number_of_attacks: int = get_action_value("number_of_attacks", 0)
	return "Attack Generator Action: " + str(damage) + " x " + str(number_of_attacks)
