# Wraps child actions, modifying their values based on the number of cards in an input cardset
# eg pick X cards, perform child actions X times
# see also ActionVariableCostModifiier
extends BaseCardsetAction

func is_instant_action() -> bool:
	return true

func perform_action():
	var action_interceptor_processors: Array[ActionInterceptorProcessor] = _intercept_action([])
	
	for action_interceptor_processor in action_interceptor_processors:
		var modified_action_data: Array[Dictionary] = []
		var action_data = action_interceptor_processor.get_shadowed_action_values("action_data", [])
		var multiplied_values: Array = action_interceptor_processor.get_shadowed_action_values("multiplied_values", [])	# the key names of the values of child actions multiplied by this action
		var multiplier_offset: int = max(0, action_interceptor_processor.get_shadowed_action_values("multiplier_offset", 0))	# an additional amount to improve the multiplier by. Eg 1 would be X + 1. Must be positive
		var multiplied_values_bases: Dictionary = action_interceptor_processor.get_shadowed_action_values("multiplied_values_bases", {})	# allows for a base value on top of modified values. eg Base + (X x Value)
		
		var picked_cards: Array[CardData] = _get_picked_cards()
		var input_energy: int = len(picked_cards)
		
		# creates a duplicate of the child action data, then modifies any keys with a multiple of the card play's input energy
		for action in action_data.duplicate(true):
			for action_script_path in action:
				for action_path in action:
					var action_values: Dictionary = action[action_script_path]
					for value_key in action_values.keys():
						if value_key in multiplied_values:
							var base_value: int = multiplied_values_bases.get(value_key, 0)
							var value: int = action_values[value_key]
							action_values[value_key] = base_value + (value * (input_energy + multiplier_offset))
			
			modified_action_data.append(action)
		
		# also modifies any card play values
		for value_key in card_play_request.card_values.keys():
			if value_key in multiplied_values:
				var base_value: int = multiplied_values_bases.get(value_key, 0)
				var value: int = card_play_request.card_values[value_key]
				card_play_request.card_values[value_key] = base_value + (value * (input_energy + multiplier_offset))
		
		var generated_actions: Array[BaseAction] = ActionGenerator.create_actions(parent_combatant, card_play_request, targets, modified_action_data, self)
		ActionHandler.add_actions(generated_actions)
