## A (usually temporary) object used to filter down an initial set of artifacts.
## These are used for things like artifact drafts or artifacts that generate random artifacts.
## Supports method chaining. ex: ArtifactFilter.new(artifacts).filter_1().filter_2().convert_to_artifact_prototypes()
## NOTE: For very large sets of artifacts, you may wish to cache the ArtifactFilter with cache_filter() and reuse it.
extends RefCounted
class_name ArtifactFilter

var filtered_artifacts: Array[ArtifactData] = []	# artifacts after filters have been applied

## Maintains all filtered_artifacts artifact_object_ids as a Set of keys for fast .has() lookups.
## Value is always null for each key.
var filtered_artifact_unique_object_ids: Dictionary[String, Variant]

## When cached, filtered_artifacts cannot be mutated with filters, essentially locking the output
var cached: bool = false

### Start of Chain

## NOTE: If you do note provide an input artifactset, the default is to use the read only artifactset of
## ALL artifacts in game. This is not only non-performant when many filters need to be applied, but
## the end result of the filter chain will still be the read-only artifacts. You will need to finish
## the chain with convert_to_artifact_prototypes() or convert_to_artifact_object_ids() or risk mutating that data.
func _init(input_artifactset: Array[ArtifactData] = Global.get_all_artifacts(), input_read_only_artifact_object_ids: Array[String] = []):
	filtered_artifacts = input_artifactset
	# if an empty artifactset is provided, try to generate one using given ids
	# of read only artifact templates
	if len(input_artifactset) == 0:
		for input_artifact_object_id: String in input_read_only_artifact_object_ids:
			var artifact_data: ArtifactData = Global.get_artifact_data(input_artifact_object_id)
			input_artifactset.append(artifact_data)
			filtered_artifact_unique_object_ids[artifact_data.object_id] = null
	else:
		for artifact_data: ArtifactData in input_artifactset:
			filtered_artifact_unique_object_ids[artifact_data.object_id] = null

### Filters

func filter_rarity(ARTIFACT_RARITIES: Array[int] = ArtifactData.ARTIFACT_RARITIES.keys(), include: bool = true) -> ArtifactFilter:
	if cached:
		return self
	
	var returned_artifacts: Array[ArtifactData] = []
	var returned_artifact_object_ids: Dictionary[String, Variant] = {}
	
	for artifact_data in filtered_artifacts:
		if ARTIFACT_RARITIES.has(artifact_data.artifact_rarity) == include:
			returned_artifacts.append(artifact_data)
			returned_artifact_object_ids[artifact_data.object_id] = null
	
	filtered_artifacts = returned_artifacts
	filtered_artifact_unique_object_ids = returned_artifact_object_ids
	return self

func filter_appears_in_artifact_packs(include: bool = true) -> ArtifactFilter:
	if cached:
		return self
	
	var returned_artifacts: Array[ArtifactData] = []
	var returned_artifact_object_ids: Dictionary[String, Variant] = {}
	
	for artifact_data: ArtifactData in filtered_artifacts:
		if artifact_data.artifact_appears_in_artifact_packs == include:
			returned_artifacts.append(artifact_data)
			returned_artifact_object_ids[artifact_data.object_id] = null
	
	filtered_artifacts = returned_artifacts
	filtered_artifact_unique_object_ids = returned_artifact_object_ids
	return self

func filter_colors(artifact_color_ids: Array[String] = [], include: bool = true) -> ArtifactFilter:
	if cached:
		return self
	if len(artifact_color_ids) == 0:
		return self
	
	var returned_artifacts: Array[ArtifactData] = []
	var returned_artifact_object_ids: Dictionary[String, Variant] = {}
	
	for artifact_data in filtered_artifacts:
		var artifact_has_color: bool = artifact_color_ids.has(artifact_data.artifact_color_id)
		
		if artifact_has_color == include:
			returned_artifacts.append(artifact_data)
			returned_artifact_object_ids[artifact_data.object_id] = null
	
	filtered_artifacts = returned_artifacts
	filtered_artifact_unique_object_ids = returned_artifact_object_ids
	return self

## Throttles the filtered artifacts to the first N results. -1 for no filtering
func first_results(artifact_amount: int = -1) -> ArtifactFilter:
	if cached:
		return self
	if artifact_amount <= 0:
		return self
		
	filtered_artifacts = filtered_artifacts.slice(0, artifact_amount)
	return self

### Include

## Forcefully includes artifacts into the artifact filter results, to be used after all filters have been
## applied. Only useful if you're using read only artifact inputs
func include_artifact_object_ids(artifact_read_only_object_ids: Array[String]) -> ArtifactFilter:
	if cached:
		return self
	
	for artifact_read_only_object_id: String in artifact_read_only_object_ids:
		if not filtered_artifact_unique_object_ids.has(artifact_read_only_object_id):
			var artifact_data: ArtifactData = Global.get_artifact_data(artifact_read_only_object_id)
			filtered_artifacts.append(artifact_data)
			filtered_artifact_unique_object_ids[artifact_data.object_id] = null
	
	return self


### Cache

## Prevents filter from being further mutated and caches it under a given id
func cache_filter(artifact_filter_cache_id: String) -> ArtifactFilter:
	cached = true
	Global.cache_artifact_filter(artifact_filter_cache_id, self)
	return self

### End of Chain

## Done at the end of chain to get prototype instances of all artifacts after filters have been applied. Allows duplicates.
func convert_to_artifact_prototypes() -> Array[ArtifactData]:
	# done at the end of a filter chain to convert the remaining artifacts into new artifacts
	var generated_artifact_prototypes: Array[ArtifactData] = []
	for artifact_data in filtered_artifacts:
		generated_artifact_prototypes.append(Global.get_artifact_data_from_prototype(artifact_data.object_id))
	return generated_artifact_prototypes

## Gets prototype instances of all unique artifacts after filters have been applied
func convert_to_unique_artifact_prototypes() -> Array[ArtifactData]:
	# done at the end of a filter chain to convert the remaining artifacts into new artifacts
	var unique_artifact_object_ids: Array[String] = convert_to_unique_artifact_object_ids()
	var generated_artifact_prototypes: Array[ArtifactData] = Global.get_artifact_data_from_prototypes(unique_artifact_object_ids)
	return generated_artifact_prototypes

## Done at the end of chain to convert the remaining artifacts into an id list. Allows duplicates.
func convert_to_artifact_object_ids() -> Array[String]:
	# done at the end of a filter chain to convert the remaining artifacts into an id list
	var artifact_object_ids: Array[String] = []
	for artifact_data in filtered_artifacts:
		artifact_object_ids.append(artifact_data.object_id)
	return artifact_object_ids

func convert_to_unique_artifact_object_ids() -> Array[String]:
	return filtered_artifact_unique_object_ids.keys().duplicate(true) # duplicated to allow immediate mutation/shuffling, as is usually the case
