## A (usually temporary) object used to filter down an initial set of cards.
## These are used for things like card drafts or cards that generate random cards.
## Supports method chaining. ex: CardFilter.new(cards).filter_1().filter_2().convert_to_card_prototypes()
## NOTE: For very large sets of cards, you may wish to cache the CardFilter with cache_filter() and reuse it.
extends RefCounted
class_name CardFilter

var filtered_cards: Array[CardData] = []	# cards after filters have been applied

## Maintains all filtered_cards card_object_ids as a Set of keys for fast .has() lookups.
## Value is always null for each key.
var filtered_card_unique_object_ids: Dictionary[String, Variant]

## When cached, filtered_cards cannot be mutated with filters, essentially locking the output
var cached: bool = false

### Start of Chain

## NOTE: If you do note provide an input cardset, the default is to use the read only cardset of
## ALL cards in game. This is not only non-performant when many filters need to be applied, but
## the end result of the filter chain will still be the read-only cards. You will need to finish
## the chain with convert_to_card_prototypes() or convert_to_card_object_ids() or risk mutating that data.
func _init(input_cardset: Array[CardData] = Global.get_all_cards(), input_read_only_card_object_ids: Array[String] = []):
	filtered_cards = input_cardset
	# if an empty cardset is provided, try to generate one using given ids
	# of read only card templates
	if len(input_cardset) == 0:
		for input_card_object_id: String in input_read_only_card_object_ids:
			var card_data: CardData = Global.get_card_data(input_card_object_id)
			input_cardset.append(card_data)
			filtered_card_unique_object_ids[card_data.object_id] = null
	else:
		for card_data: CardData in input_cardset:
			filtered_card_unique_object_ids[card_data.object_id] = null

### Filters

## Main method of generically filtering cards. Takes an array of data for BaseValidator scripts and
## parameters, applying them to each card, then narrowing down filtered_cards
func filter_card_validators(card_validators: Array) -> CardFilter:
	if cached:
		return self
	# general filter that applies arbitrary validators
	for validator_data: Dictionary in card_validators:
		for validator_script_path: String in validator_data:
			# generate validator
			var validator_script_asset = load(validator_script_path)
			var validator: BaseValidator = validator_script_asset.new()
			
			var validator_values: Dictionary[String, Variant] = {}
			validator_values.assign(validator_data[validator_script_path])

			# iterate over each card in the validator amd filter it
			var validator_result: Array[CardData] = []
			var validator_id_result: Dictionary[String, Variant] = {}
			
			for card_data: CardData in filtered_cards:
				if validator.validate(card_data, null, validator_values):
					validator_result.append(card_data)
					validator_id_result[card_data.object_id] = null
				
			filtered_cards = validator_result
			filtered_card_unique_object_ids = validator_id_result
	
	return self
	
	

func filter_type(card_types: Array[int] = CardData.CARD_TYPES.keys(), include: bool = true) -> CardFilter:
	if cached:
		return self
	
	var returned_cards: Array[CardData] = []
	var returned_card_object_ids: Dictionary[String, Variant] = {}
	
	for card_data in filtered_cards:
		if card_types.has(card_data.card_type) == include:
			returned_cards.append(card_data)
			returned_card_object_ids[card_data.object_id] = null
	
	filtered_cards = returned_cards
	filtered_card_unique_object_ids = returned_card_object_ids
	return self

func filter_standard_type() -> CardFilter:
	return filter_type(CardData.STANDARD_CARD_TYPES)

func filter_rarity(card_rarities: Array[int] = CardData.CARD_RARITIES.keys(), include: bool = true) -> CardFilter:
	if cached:
		return self
	
	var returned_cards: Array[CardData] = []
	var returned_card_object_ids: Dictionary[String, Variant] = {}
	
	for card_data in filtered_cards:
		if card_rarities.has(card_data.card_rarity) == include:
			returned_cards.append(card_data)
			returned_card_object_ids[card_data.object_id] = null
	
	filtered_cards = returned_cards
	filtered_card_unique_object_ids = returned_card_object_ids
	return self

func filter_appears_in_card_packs(include: bool = true) -> CardFilter:
	if cached:
		return self
	
	var returned_cards: Array[CardData] = []
	var returned_card_object_ids: Dictionary[String, Variant] = {}
	
	for card_data in filtered_cards:
		if card_data.card_appears_in_card_packs == include:
			returned_cards.append(card_data)
			returned_card_object_ids[card_data.object_id] = null
	
	filtered_cards = returned_cards
	filtered_card_unique_object_ids = returned_card_object_ids
	return self

func filter_energy_cost(card_energy_costs: Array[int], variable_energy_cost: bool = false, include: bool = true) -> CardFilter:
	if cached:
		return self
	
	var returned_cards: Array[CardData] = []
	var returned_card_object_ids: Dictionary[String, Variant] = {}
	
	for card_data in filtered_cards:
		var card_matches_variable_cost: bool = (card_data.card_energy_cost_is_variable and variable_energy_cost)
		var card_has_energy_cost: bool = card_energy_costs.has(card_data.get_card_energy_cost())
		
		if (card_matches_variable_cost or card_has_energy_cost) == include:
			returned_cards.append(card_data)
			returned_card_object_ids[card_data.object_id] = null
	
	filtered_cards = returned_cards
	filtered_card_unique_object_ids = returned_card_object_ids
	return self

func filter_colors(card_color_ids: Array[String] = [], include: bool = true) -> CardFilter:
	if cached:
		return self
	if len(card_color_ids) == 0:
		return self
	
	var returned_cards: Array[CardData] = []
	var returned_card_object_ids: Dictionary[String, Variant] = {}
	
	for card_data in filtered_cards:
		var card_has_color: bool = card_color_ids.has(card_data.card_color_id)
		
		if card_has_color == include:
			returned_cards.append(card_data)
			returned_card_object_ids[card_data.object_id] = null
	
	filtered_cards = returned_cards
	filtered_card_unique_object_ids = returned_card_object_ids
	return self

## Throttles the filtered cards to the first N results. -1 for no filtering
func first_results(card_amount: int = -1) -> CardFilter:
	if cached:
		return self
	if card_amount <= 0:
		return self
		
	filtered_cards = filtered_cards.slice(0, card_amount)
	return self

### Include

## Forcefully includes cards into the card filter results, to be used after all filters have been
## applied. Only useful if you're using read only card inputs
func include_card_object_ids(card_read_only_object_ids: Array[String]) -> CardFilter:
	if cached:
		return self
	
	for card_read_only_object_id: String in card_read_only_object_ids:
		if not filtered_card_unique_object_ids.has(card_read_only_object_id):
			var card_data: CardData = Global.get_card_data(card_read_only_object_id)
			filtered_cards.append(card_data)
			filtered_card_unique_object_ids[card_data.object_id] = null
	
	return self


### Cache

## Prevents filter from being further mutated and caches it under a given id
func cache_filter(card_filter_cache_id: String) -> CardFilter:
	cached = true
	Global.cache_card_filter(card_filter_cache_id, self)
	return self

### End of Chain

## Done at the end of chain to get prototype instances of all cards after filters have been applied. Allows duplicates.
func convert_to_card_prototypes() -> Array[CardData]:
	# done at the end of a filter chain to convert the remaining cards into new cards
	var generated_card_prototypes: Array[CardData] = []
	for card_data in filtered_cards:
		generated_card_prototypes.append(Global.get_card_data_from_prototype(card_data.object_id))
	return generated_card_prototypes

## Gets prototype instances of all unique cards after filters have been applied
func convert_to_unique_card_prototypes() -> Array[CardData]:
	# done at the end of a filter chain to convert the remaining cards into new cards
	var unique_card_object_ids: Array[String] = convert_to_unique_card_object_ids()
	var generated_card_prototypes: Array[CardData] = Global.get_card_data_from_prototypes(unique_card_object_ids)
	return generated_card_prototypes

## Done at the end of chain to convert the remaining cards into an id list. Allows duplicates.
func convert_to_card_object_ids() -> Array[String]:
	# done at the end of a filter chain to convert the remaining cards into an id list
	var card_object_ids: Array[String] = []
	for card_data in filtered_cards:
		card_object_ids.append(card_data.object_id)
	return card_object_ids

func convert_to_unique_card_object_ids() -> Array[String]:
	return filtered_card_unique_object_ids.keys().duplicate(true) # duplicated to allow immediate mutation/shuffling, as is usually the case
