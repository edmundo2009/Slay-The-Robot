## Read only data used to generate a list of artifacts, which are converted into a cached ArtifactFilter on game start in Global.
## This reduces repeated expensive queries across the entire pool of artifacts and allows for dynamically
## generating lists instead of harder to maintain id listings.
extends SerializableData
class_name ArtifactPackData

## Allows explicitly defining artifacts to be included. These are included AFTER filtering by color and
## validators.
@export var artifact_pack_artifact_ids: Array[String] = []

## Provides a shorthand for filtering artifacts by color
@export var artifact_pack_color_id: String = ""

## Prevents rarities other than ones defined by ArtifactData.STANDARD_ARTIFACT_RARITIES.
@export var exclude_non_standard_rarities = false

## Creates a artifact filter using this artifact pack
func create_artifact_pack_artifact_filter() -> ArtifactFilter:
	var artifact_filter: ArtifactFilter = ArtifactFilter.new()
	if artifact_pack_color_id != "":
		artifact_filter = artifact_filter.filter_colors([artifact_pack_color_id])
	if exclude_non_standard_rarities:
		artifact_filter = artifact_filter.filter_rarity(ArtifactData.STANDARD_ARTIFACT_RARITIES)
	artifact_filter = artifact_filter.filter_appears_in_artifact_packs(true)
	artifact_filter = artifact_filter.include_artifact_object_ids(artifact_pack_artifact_ids)
	
	return artifact_filter
