## Read only data for a list of cards, which are converted into a cached CardFilter on game start in Global.
## This reduces repeated expensive queries across the entire pool of cards and allows for dynamically
## generating lists instead of harder to maintain id listings.
extends SerializableData
class_name CardPackData

## Allows explicitly defining cards to be included. These are included AFTER filtering by color and
## validators.
@export var card_pack_card_ids: Array[String] = []

## Provides a shorthand for filtering cards by color
@export var card_pack_color_id: String = ""

## Prevents rarities other than ones defined by CardData.STANDARD_CARD_RARITIES. Usually turned off
## for packs that use generated cards or statuses.
@export var exclude_non_standard_rarities = true
## Prevents types other than ones defined by CardData.STANDARD_CARD_TYPES. Usually turned off
## for packs that use generated cards or statuses.
@export var exclude_non_standard_types = true

## Additional validators to be used.
@export var card_pack_validators: Array[Dictionary] = [] # validators required for the action to be clickable

## Creates a card filter using this card pack
func create_card_pack_card_filter() -> CardFilter:
	var card_filter: CardFilter = CardFilter.new()
	if card_pack_color_id != "":
		card_filter = card_filter.filter_colors([card_pack_color_id])
	if exclude_non_standard_rarities:
		card_filter = card_filter.filter_rarity(CardData.STANDARD_CARD_RARITIES)
	if exclude_non_standard_types:
		card_filter = card_filter.filter_type(CardData.STANDARD_CARD_TYPES)
	card_filter = card_filter.filter_appears_in_card_packs(true)
	card_filter = card_filter.filter_card_validators(card_pack_validators).include_card_object_ids(card_pack_card_ids)
	
	return card_filter
