## Read only data object that maintains a list of vanilla/mod base folder locations and whether
## that folder is enabled/disabled or not for modloading purposes.
## Each folder it points to should contain a mod_info.json file which corresponds to a ModData
## This file is loaded on game start.
extends SerializableData
class_name ModListData

## Maps directory local filepaths (located in the "external" folder) to metadata on how to load
## various mods contained in those subdirectories.
@export var mod_load_data: Dictionary = {
	#"<mod_folder_path>": {
		#"enabled": true, # if the mod is enabled for loading or not
		#"load_priority": 0, # Non-negative numbers only. Higher priority numbers will be loaded last. 0 is typically base game
	#}
}


## Returns a list of mod folder keys for all enabled mods, sorted by load priority.
func get_sorted_enabled_mod_folder_list() -> Array[String]:
	var enabled_mod_folders: Array[String] = []
	
	# get all enabled mods
	for mod_folder_path: String in mod_load_data.keys():
		var mod_data: Dictionary = mod_load_data[mod_folder_path]
		
		var enabled: bool = mod_data.get("enabled", true)
		if enabled:
			enabled_mod_folders.append(mod_folder_path)
			
	# sort them by load priority
	enabled_mod_folders.sort_custom(_mod_priority_sort)
	
	return enabled_mod_folders

## Private helper method for get_sorted_enabled_mod_folder_list()
func _mod_priority_sort(mod_folder_path_1: String, mod_folder_path_2: String) -> bool:
	var mod_data_1: Dictionary = mod_load_data[mod_folder_path_1]
	var mod_data_2: Dictionary = mod_load_data[mod_folder_path_2]
	var load_priority_1: int = mod_data_1.get("load_priority", 0)
	var load_priority_2: int = mod_data_2.get("load_priority", 0)

	return load_priority_1 < load_priority_2
