## Read only data. Defines an event, which can be combat or dialogue at a LocationData. These events
## are typically pulled from an EventPoolData via PlayerData.get_next_event_object_id_from_pool()
## upon visiting a location (ensuring the same order each time). See LocationData.
extends SerializableData
class_name EventData

## Validator data used to determine if the event is valid for use to pull from a pool when the location
## is visited. This can be used to filter events based on given criteria such as not showing up
## past a difficulty level, requiring a certain amount of health/money, etc.
@export var event_pool_validator_data: Array[Dictionary] = [
	#{
		#"validator_script_file_path_1": {"validator_key_1": "value"}
	#},
	#{
		#"validator_script_file_path_2": {"validator_key_1": "value"}
	#}	
]

## See location_event_pool_validator_failed_strategy
enum FailedEventPoolStrategies {
	KEEP, # the event is held in place at the front of the pool.
	REMOVE, # the event is removed from the pool and cannot show up again until the pool is repopulated
	APPEND, # the event is reinserted at the end of the pool
	REINSERT, # the event is randomly inserted into a different part of the pool
	BLACKLIST, # the event is blacklisted for the rest of the run and cannot reappear even on pool repopulation
}

## If an event being pulled from a pool fails to pass the validators, the strategy determines what to do
## to that event in the pool. Typically it will involve removal/keeping, though you may wish to invoke other
## strategies. Note that strategies that modify the pool will be done after the event selection to avoid
## any weirdness with iteration of the pool.
@export var location_event_pool_validator_failed_strategy: int = FailedEventPoolStrategies.REMOVE

## If the event/location is combat, these are the enemies that will spawn.
## See event_enemy_placement_is_automatic and event_enemy_placement_positions.
## Each slot is a weighted mapping of the probability of that enemy spawning
@export var event_weighted_enemy_object_ids: Array[Dictionary] = [
#	{"enemy_id_1": weight_1, "enemy_id_2": weight_2}
	{"enemy_1": 1, "enemy_2": 1},
	{"enemy_2": 1},
	]

## Determines whether to use an hbox for automatic placement of enemies, or
## positional data for enemies in unique spots on the screen via event_enemy_placement_positions
@export var event_enemy_placement_is_automatic: bool = true
## Array of positions for where each enemy should go if event_enemy_placement_is_automatic is false.
## This is accessed both in parallel to event_weighted_enemy_object_ids and direct indexed when summoning additional
## enemies via ActionSummonEnemies.
@export var event_enemy_placement_positions: Array[Array] = [[0,-40], [0,40]]

## Actions that trigger at the start of combat for this event.
@export var event_initial_combat_actions: Array[Dictionary] = []

## A path to an external texture file to use when doing this event. Overrides the act and locationn background
@export var event_background_texture_path: String = ""

## Corresponds to a DialogueData for this event. Only matters if the LocationData's location_type
## is EVENT. See: DialogueOverlay
@export var event_dialogue_object_id: String = ""


## Checks if event passes all validators to be eligable for use for an event pool
func validate_event() -> bool:
	return Global.validate(event_pool_validator_data, null, null)
