## Maintains all inventory and metadata for a shop.
## Embedded data within PlayerData
extends SerializableData
class_name ShopData

@export var shop_is_visited: bool = false	# determines whether to perform actions for the first time

## The location this shop belongs to. Used to validate if the shop actually belongs to the player's
## current location
@export var shop_location_id: String = ""

# items for purchase
@export var shop_cards: Array[CardData] = [] # array of CardData prototype instances
@export var shop_artifact_ids: Array[String] = []
@export var shop_consumable_slot_to_consumable_object_id: Dictionary = {}	# maps a numerical slot index to a consumable id. 0 indexed

# prices; parallel to the items
@export var shop_card_prices: Array[int] = []
@export var shop_artifact_prices: Array[int] = []
@export var shop_consumable_slot_to_consumable_price: Dictionary = {}

const GENERATED_CARD_COUNT: int = 6
const GENERATED_ARTIFACT_COUNT: int = 2
const GENERATED_SHOP_SPECIFIC_ARTIFACT_COUNT: int = 1
const GENERATED_CONSUMABLE_COUNT: int = 3

# price ranges
const CARD_RARITY_TO_PRICE_RANGE: Dictionary = {
	CardData.CARD_RARITIES.COMMON: [50,80],
	CardData.CARD_RARITIES.UNCOMMON: [85,115],
	CardData.CARD_RARITIES.RARE: [120,140],
}

const ARTIFACT_RARITY_TO_PRICE_RANGE: Dictionary = {
	ArtifactData.ARTIFACT_RARITIES.COMMON: [50,80],
	ArtifactData.ARTIFACT_RARITIES.UNCOMMON: [85,115],
	ArtifactData.ARTIFACT_RARITIES.RARE: [120,140],
	ArtifactData.ARTIFACT_RARITIES.BOSS: [130,150],
	ArtifactData.ARTIFACT_RARITIES.SHOP: [150,200],
	ArtifactData.ARTIFACT_RARITIES.EVENT: [0,1],
}

const CONSUMABLE_RARITY_TO_PRICE_RANGE: Dictionary = {
	ConsumableData.CONSUMABLE_RARITIES.COMMON: [50,80],
	ConsumableData.CONSUMABLE_RARITIES.UNCOMMON: [85,115],
	ConsumableData.CONSUMABLE_RARITIES.RARE: [120,140],
	ConsumableData.CONSUMABLE_RARITIES.LEGENDARY: [130,150],
}

## Populates a given shop with items if it has not been visited yet.
func visit_shop() -> void:
	
	if not shop_is_visited:
		# get shop rng
		var rng_shop: RandomNumberGenerator = Global.player_data.get_player_rng("rng_shop")
		
		### Generate Items
		# generates shop cards
		var generated_cards: Array[CardData] = Random.generate_rarity_weighted_card_draft(rng_shop, ShopData.GENERATED_CARD_COUNT, Random.CARD_DRAFT_TABLE_TYPES.SHOP, false)
		
		# generate regular artifacts from player artifact pool
		var artifact_ids: Array[String] = Global.player_data.get_next_shop_standard_artifacts_from_pool(GENERATED_ARTIFACT_COUNT, true)
		
		# generate shop artifacts from player artifact pool
		var shop_artifact_ids: Array[String] = Global.player_data.get_next_shop_specific_artifacts_from_pool(GENERATED_SHOP_SPECIFIC_ARTIFACT_COUNT, true)
		artifact_ids.append_array(shop_artifact_ids)
		
		# generate shop consumables
		var consumable_ids: Array[String] = []
		for _i in GENERATED_CONSUMABLE_COUNT:
			var consumable_object_id: String = Random.get_random_consumable_object_id(rng_shop)
			consumable_ids.append(consumable_object_id)
		
		### Generate Prices
		var card_prices: Array[int] = Random.get_shop_card_prices(generated_cards, rng_shop)
		var artifact_prices: Array[int] = Random.get_shop_artifact_prices(artifact_ids, rng_shop)
		var consumable_prices: Array[int] = Random.get_shop_consumable_prices(consumable_ids, rng_shop)
		
		### Generate Population Action
		ActionGenerator.generate_populate_shop_items(generated_cards, card_prices, artifact_ids, artifact_prices, consumable_ids, consumable_prices)
		
		### First Time Visit
		shop_is_visited = true # flags shop, ensuring this method can only be called once
		Signals.shop_visited_first_time.emit() 

### Shop Cards ###

func add_shop_card_from_id(card_object_id: String, price: int = -1) -> void:
	var card_data: CardData = Global.get_card_data(card_object_id)
	if card_data != null:
		add_shop_card(card_data, price)

func add_shop_card(card_data: CardData, price: int = -1):
	# adds a card and randomizes the price, if no price is given
	if card_data != null:
		var card_price_range_values: Array = CARD_RARITY_TO_PRICE_RANGE.get(card_data.card_rarity)
		var item_price_range: int = card_price_range_values[1] - card_price_range_values[0]
		var card_price: int = price
		if price < 0:
			var rng_shop: RandomNumberGenerator = Global.player_data.get_player_rng("rng_shop")
			card_price = card_price_range_values[0] + (rng_shop.randi() % item_price_range)
		
		shop_card_prices.append(card_price)
		shop_cards.append(card_data)
	else:
		breakpoint

func remove_shop_card(card_data: CardData) -> void:
	var index: int = shop_cards.find(card_data)
	if index != -1:
		shop_cards.remove_at(index)
		shop_card_prices.remove_at(index)

func get_shop_card_price(card_data: CardData) -> int:
	if shop_cards.has(card_data):
		return shop_card_prices[shop_cards.find(card_data)]
	return 0

### Shop Artifacts ###

func add_shop_artifact(artifact_id: String, price: int = -1) -> void:
	# adds a artifact and randomizes the price, if no price is given
	var artifact_data: ArtifactData = Global.get_artifact_data(artifact_id)
	if artifact_data != null:
		var artifact_price_range_values: Array = ARTIFACT_RARITY_TO_PRICE_RANGE.get(artifact_data.artifact_rarity)
		var item_price_range: int = artifact_price_range_values[1] - artifact_price_range_values[0]
		var artifact_price: int = price
		if price < 0:
			var rng_shop: RandomNumberGenerator = Global.player_data.get_player_rng("rng_shop")
			artifact_price = artifact_price_range_values[0] + (rng_shop.randi() % item_price_range)
		
		shop_artifact_prices.append(artifact_price)
		shop_artifact_ids.append(artifact_data.object_id)
	else:
		breakpoint

func remove_shop_artifact(artifact_id: String) -> void:
	var index: int = shop_artifact_ids.find(artifact_id)
	if index != -1:
		shop_artifact_ids.remove_at(index)
		shop_artifact_prices.remove_at(index)

func get_shop_artifact_price(artifact_id: String) -> int:
	if shop_artifact_ids.has(artifact_id):
		return shop_artifact_prices[shop_artifact_ids.find(artifact_id)]
	return 0

func get_shop_artifact_options() -> Array[ArtifactData]:
	# returns array of data for purchasable artifacts
	var artifacts: Array[ArtifactData] = []
	for artifact_id in shop_artifact_ids:
		var artifact_data: ArtifactData = Global.get_artifact_data_from_prototype(artifact_id)
		artifacts.append(artifact_data)
	return artifacts

### Shop Consumables ###

func add_shop_consumable(consumable_object_id: String, price: int = -1):
	# adds a consumable and randomizes the price, if no price is given
	var consumable_data: ConsumableData = Global.get_consumable_data(consumable_object_id)
	if consumable_data != null:
		var consumable_price_range_values: Array = CONSUMABLE_RARITY_TO_PRICE_RANGE.get(consumable_data.consumable_rarity)
		var item_price_range: int = consumable_price_range_values[1] - consumable_price_range_values[0]
		var consumable_price: int = price
		if price < 0:
			var rng_shop: RandomNumberGenerator = Global.player_data.get_player_rng("rng_shop")
			consumable_price = consumable_price_range_values[0] + (rng_shop.randi() % item_price_range)
		
		var slot_index: int = get_next_empty_shop_consumable_slot()
		
		shop_consumable_slot_to_consumable_object_id[slot_index] = consumable_object_id
		shop_consumable_slot_to_consumable_price[slot_index] = consumable_price
	else:
		breakpoint

func get_next_empty_shop_consumable_slot() -> int:
	var slot_index: int = 0
	while(true):
		if not shop_consumable_slot_to_consumable_object_id.has(slot_index):
			break
		slot_index += 1
	return slot_index

func remove_shop_consumable(consumable_slot_index: int) -> void:
	shop_consumable_slot_to_consumable_object_id.erase(consumable_slot_index)
	shop_consumable_slot_to_consumable_price.erase(consumable_slot_index)

func get_shop_consumable_price(consumable_slot_index: int) -> int:
	return shop_consumable_slot_to_consumable_price.get(consumable_slot_index, 0)
