## Provides same functionality as SerializeableData with support for prototype pattern
extends SerializableData
class_name PrototypeData

## Dynamically generated unique identifier
## Combination of class name, time, and a counter to avoid collisions while being human readable
## In prototyped objects the object_id is the subtype, while the object_uid distinguishes subtypes
## Eg for CarData object_id = "car_red_with_stripes" and object_uid will be a unique instance of car_red_with_stripes
## There should exist a source object for each subtype as a source of truth stored globally. By convention its object_uid is empty
## This source of truth is loaded from config data on game load and instances made from it via get_prototype before being modified
@export var object_uid: String = ""

func get_unique_id_prefix() -> String:
	## returns a prefix used in unique ID string generation
	## used in UIDGenerator
	var script: Script = get_script()
	return script.get_global_name().to_lower().replace("data","").to_pascal_case()

func get_prototype(duplicate_sub_prototypes: bool = true) -> PrototypeData:
	# returns a copy of this object, with a new object_uid for the copy
	var prototype: PrototypeData = duplicate(true)
	prototype.object_uid = UIDGenerator.generate_unique_id(self)
	
	# generates new prototypes for all sub objects
	if duplicate_sub_prototypes:
		var old_uid_to_new_uid: Dictionary[String, String] = {} # maintain a mapping of sub prototypes duplicated
		# duplicate all contained recursive prototype data as well
		var recursive_properties: Dictionary = _get_recursive_properties()
		for property_name: String in recursive_properties:
			var current_value: Variant = get(property_name)
			# figure out if value is Array[PrototypeData], Dictionary[String, PrototypeData], or PrototypeData
			if current_value is Array:
				if len(current_value) == 0:
					continue # empty array, no need to copy
				if not current_value[0] is PrototypeData:
					continue # recursive object is not a PrototypeData
				# populate the new array with copied prototypes
				var new_array: Array = []
				for sub_prototype_data: PrototypeData in current_value:
					var copied_sub_prototype: PrototypeData = sub_prototype_data.get_prototype()
					new_array.append(copied_sub_prototype)
					old_uid_to_new_uid[sub_prototype_data.object_uid] = copied_sub_prototype.object_uid # map old object to new object for repairs
				# assign array to maintain type
				var temp_value = prototype.get(property_name)
				temp_value.clear()
				temp_value.assign(new_array)
				prototype.set(property_name, temp_value)
			elif current_value is Dictionary:
				# dictionary
				if len(current_value.keys()) == 0:
					continue # empty dict, no need to copy
				if not current_value[current_value.keys()[0]] is PrototypeData:
					continue # not a Dict[PrototypeData]
				# overwrite the values in the new dict with copied sub objects
				var new_dictionary: Dictionary = {}
				for prototype_uid: String in current_value.keys():
					var sub_prototype_data: PrototypeData = current_value[prototype_uid]
					var copied_sub_prototype: PrototypeData = sub_prototype_data.get_prototype()
					var new_uid: String = copied_sub_prototype.object_uid
					new_dictionary[new_uid] = copied_sub_prototype # assign a new entry under the new object's uid
					old_uid_to_new_uid[sub_prototype_data.object_uid] = copied_sub_prototype.object_uid # map old object to new object for repairs
				
				var temp_value = prototype.get(property_name)
				temp_value.clear()
				temp_value.assign(new_dictionary)
				prototype.set(property_name, temp_value)
			else:
				# assume single type field
				if current_value == null:
					continue
				elif current_value is PrototypeData:
					var sub_prototype_data: PrototypeData = current_value.get_prototype()
					prototype.set(property_name, sub_prototype_data)
		
		# repair internal uids using newly created sub objects
		prototype._repair_internal_uids(old_uid_to_new_uid)
	
	# return duplicated object
	return prototype 

## Optional Override. This method is called when get_prototype() is called after all sub objects are created. A mapping is
## created mapping the UIDs of the old prototypes to the ones in the new object. This allows you to
## repair any secondary variables that were 1:1 copied using String UIDs that used the old UIDs and
## thus break on object duplication.
func _repair_internal_uids(old_uid_to_new_uid: Dictionary[String, String]) -> void:
	pass
